uses 'ExternalQueue.feh';

namespace ClearItPrototype {
	function invoke( string url, string f, array parameters, string result ) {
		array body = [ "${f}Response" => [ "${f}Return" => result ] ];
		string message;
		
		message += "<?xml version='1.0' ?>";
		message += '<soapenv:Envelope xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"';
		message += ' xmlns:xsd="http://www.w3.org/2001/XMLSchema"';
		message += ' xmlns:soapenv="http://schemas.xmlsoap.org/soap/envelope/"';
		message += ' xmlns:ser="services.soap.uqf.clearit.se">';
		
		message += '<soapenv:Body>';
		message += "<ser:${f} soapenv:encodingStyle=\"http://schemas.xmlsoap.org/soap/encoding/\">";
		
		parameters.eachWithIndex() using ( value, index ) {
			string name = parameters.getName(index);
			message += SOAP.toSOAP(name, value, '');
		};
		
		message += "</ser:${f}>";
		message += '</soapenv:Body>';
		message += '</soapenv:Envelope>';
		
		return SOAP.RPC.sendRequest(url, '', message, body);
	}
	
	function queueItemAndForget( string taskIdentifier, array data ) {
		return .invoke(
			'http://217.151.206.149:8080/axis/services/UqfCention',
			'queueItemAndForget',
			[ 'taskIdentifier' => taskIdentifier, 'data' => data.join(';') ],
			'number'
		);
	}
	
	function queueItem( string taskIdentifier, array data, string creationTime ) {
		return .invoke(
			'http://217.151.206.149:8080/axis/services/UqfCention',
			'queueItem',
			[ 'taskIdentifier' => taskIdentifier, 'data' => data.join(';'), 'creationTime' => creationTime ],
			'number'
		);
	}
	
	function queueCentionItem( number ItemId, string taskIdentifier, string url, string errandId, string area, string channel, string creationTime ) {
		return .invoke(
			'http://217.151.206.149:8080/axis/services/UqfCention',
			'queueCentionItem',
			[
				'ItemId' => ItemId,
				'taskIdentifier' => taskIdentifier,
				'url' => url,
				'errandId' => errandId,
				'area' => area,
				'channel' => channel,
				'creationTime' => creationTime
			],
			'number'
		);
	}
	
	function endItem( number itemId, number reason ) {
		return .invoke(
			'http://217.151.206.149:8080/axis/services/UqfCention',
			'endItem',
			[ 'itemId' => itemId, 'reason' => reason ],
			'number'
		);
	}
	
	function endCentionItem( number itemId, number reason ) {
		return .invoke(
			'http://217.151.206.149:8080/axis/services/UqfCention',
			'endCentionItem',
			[ 'itemId' => itemId, 'reason' => reason ],
			'number'
		);
	}
}

class ClearItQueue implements ExternalQueue {
	static function name() {
		return 'clearit';
	}
	
	static function add( object errand ) {
		number id = ClearItPrototype.queueCentionItem(
				errand.externalID,
				'cention',
				'http://217.151.206.145/workflow/-/external/open/' + errand.id,
				'errand/' + errand.id,
				errand.targetArea.name + '/' + errand.targetArea.id,
				errand.service.name,
				Date.localDate(errand.timestampArrive + Date.timezone()).format('%F %H:%M')
			);
		if( id > 0 ) {
			errand.setValue('queuedInExternal', true);
			errand.setValue('externalID', id);
			errand.save();
			return true;
		}
		return false;
	}
	
	static function remove( object errand ) {
		number id = ClearItPrototype.endCentionItem(errand.externalID, 0);
		if( id > 0 ) {
			errand.setValue('queuedInExternal', false);
			errand.save();
			return true;
		}
		return false;
	}
}

