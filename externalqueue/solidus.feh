uses 'externalqueue';
uses 'logger';
uses 'SOAP';

class AddRequestResult {
	number EWT;
	number OpenMediaID;
	number QueuePosition;
	number RequestStatus;
	
	[rpc_import 'EWT:number', 'OpenMediaID:number', 'QueuePosition:number', 'RequestStatus:string'];
}

class GetOpenMediaRequestStatusByIDResult {
	string TimeStamp;
	array OpenMediaRequests;
	
	[rpc_import 'TimeStamp:string', 'OpenMediaRequests:array<OpenMediaRequest>'];
}

class OpenMediaRequest {
	number ID;
	number AgentID;
	string LogonID;
	string Status;
	number TenantID;
	string TimeStamp;
	
	[rpc_import 'AgentID:number', 'ID:number' , 'LogonID:number' , 'Status:string' , 'TenantID:number' , 'TimeStamp:string'];
}

class IVRInformation {
	string Data;
	string Label;
	
	constructor( string label, string data ) {
		.Label = label;
		.Data = data;
	}
	
	[rpc_export Data, Label];
}

namespace Solidus {
	static string serverAddress;
	static string soapActionURL;
	
	function setDefaults( string serverAddress, string soapActionURL ) {
		.serverAddress = serverAddress;
		.soapActionURL = soapActionURL;
	}
	
	function sendRequest( string url, string action, string f, array parameters, string result, string resultName ) {
		array body = [ "${f}Response" => [ "${resultName}" => result ] ];
		string message;
		
		message += "<?xml version='1.0' ?>";
		message += '<soap:Envelope xmlns:soap="http://schemas.xmlsoap.org/soap/envelope/"';
		message += ' xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"';
		message += ' xmlns:xsd="http://www.w3.org/2001/XMLSchema">';
		message += '<soap:Body>';
		message += "<${f} xmlns=\"${.soapActionURL}/\">";
		
		if( f != 'GetOpenMediaRequestStatusByID' )
			message += '<request>';
		
		parameters.eachWithIndex() using ( value, index ) {
			string name = parameters.getName(index);
			message += SOAP.toSOAP( name, value, (f != 'GetOpenMediaRequestStatusByID' ? 'http://schemas.datacontract.org/2004/07/Solidus.OpenMedia.Contracts.DataContracts' : '') );
		};
		
		if( f != 'GetOpenMediaRequestStatusByID' )
			message += '</request>';
		
		message += "</${f}>";
		message += '</soap:Body>';
		message += '</soap:Envelope>';
		
		return SOAP.RPC.sendRequest( url, action, message, body );
	}
	function addRequest( number serviceGroupID, string privateData ) {
		return .addRequest(serviceGroupID, 0, false, privateData, []);
	}
	function addRequest( number serviceGroupID, number preferredAgentID, boolean forceToPreferredAgent, string privateData ) {
		return .addRequest(serviceGroupID, preferredAgentID, forceToPreferredAgent, privateData, []);
	}
	function addRequest( number serviceGroupID, number preferredAgentID, boolean forceToPreferredAgent, string privateData, array ivrInfo ) {	
		object result;

		monitor {
			array parameters;

			parameters['ForceToPreferredAgent'] = forceToPreferredAgent;
			parameters['IVRInfo'] = [ new IVRInformation('label1', 'data1'), new IVRInformation(), new IVRInformation() ];
			parameters['PreferredAgentID'] = preferredAgentID;
			parameters['PrivateData'] = privateData;
			parameters['ServiceGroupID'] = serviceGroupID;
			parameters['ServiceGroupName'] = '';
			parameters['TenantID'] = -1;

			ivrInfo.eachWithIndex() using ( value, index ) {
				parameters['IVRInfo'][index].Label = ivrInfo.getName(index);
				parameters['IVRInfo'][index].Data = '' + value;
			};

			result = .sendRequest(
					.serverAddress + '/OpenMediaService',
					.soapActionURL + '/IOpenMediaService/AddRequest',
					'AddRequest',
					parameters,
					'AddRequestResult',
					'AddRequestResult'
				);
		} handle {
			result = null;
		}
		
		return result;
	}

	function cancelRequest( number openMediaID ) {
		return .cancelRequest(openMediaID, true, false);
	}
	function cancelRequest( number openMediaID, boolean cancelIfAllocated ) {
		return .cancelRequest(openMediaID, cancelIfAllocated, false);
	}
	function cancelRequest( number openMediaID, boolean cancelIfAllocated, boolean doNotReport ) {	
		boolean result;
		
		monitor {
			array parameters;
			
			parameters['CancelIfAllocated'] = cancelIfAllocated;
			parameters['DoNotReport'] = doNotReport;
			parameters['OpenMediaID'] = openMediaID;
			
			result = .sendRequest(
				.serverAddress + '/OpenMediaService',
				.soapActionURL + '/IOpenMediaService/CancelRequest',
				'CancelRequest',
				parameters,
				'boolean',
				'CancelRequestResult'
			);
		} handle {
			result = false;
		}
		
		return result;
	}

	function getOpenMediaRequestStatus( number openMediaID ) {
		object result;

		monitor {
			array parameters;
		
			parameters['openMediaID'] = openMediaID;
			
			result = .sendRequest(
				.serverAddress + '/OpenMediaService',
				.soapActionURL + '/IOpenMediaService/GetOpenMediaRequestStatusByID',
				'GetOpenMediaRequestStatusByID',
				parameters,
				'GetOpenMediaRequestStatusByIDResult',
				'GetOpenMediaRequestStatusByIDResult'
			);
		} handle {
			result = null;
		}
	
		return result;
	}
}

class SolidusQueue implements ExternalQueue {
	static string SystemBaseURL;
	
	static function name() {
		return 'solidus';
	}
	static function configure( array options ) {
		if( options.keyExists('server-address') )
			Solidus.setDefaults(options['server-address'], 'http://tempuri.org');
		if( options.keyExists('open-errand-url') )
			.SystemBaseURL = options['open-errand-url'];
	}
	
	static function addErrand( object errand, object user ) {
		object area = Workflow.Area.load(errand.targetArea.id);
		
		string privateData = .SystemBaseURL + '/solidus/login/-/errand/' + errand.id;
		number preferredAgentID = (user ? user.solidusAgentID : 0);
		boolean forceToPreferredAgent = (preferredAgentID ? true : false);
		array ivrInfo = [
			'Errand' => '' + errand.id,
			'From' => (errand.mail and errand.mail.from
				? (errand.mail.from.name
					? '{0} ({1})'.expand(errand.mail.from.name, errand.mail.from.emailAddress)
					: errand.mail.from.emailAddress)
				: 'No Sender'),
			'Subject' => (errand.message ? errand.message.subject : 'No Subject')
		];
		
		object result;
		
		Logger.info(Logger.APPLICATION_SOLIDUS, "Sending add request with preferredAgentID as ${preferredAgentID} for Workflow.Errand(${errand.id}).");
		
		result = Solidus.addRequest(area.solidusServiceGroupID, preferredAgentID, forceToPreferredAgent, privateData, ivrInfo);
		if( result and result.OpenMediaID > 0 ) {
			Logger.info(Logger.APPLICATION_SOLIDUS, "Workflow.Errand(${errand.id}) have been successfully queued as ${result.OpenMediaID}. Queue position is ${result.QueuePosition}, EWT is ${result.EWT}.");
			errand.setValue('queuedInExternal', true);
			errand.setValue('solidusOpenMediaID', result.OpenMediaID);
			errand.save();
			return true;
		}
		else {
			Logger.info(Logger.APPLICATION_SOLIDUS, "Failed to add Workflow.Errand(${errand.id}) to queue.");
			return false;
		}
	}
	
	static function removeErrand( object errand, number type ) {
		return ._remove(errand, true);
	}
	static function _remove( object errand, boolean cancelIfAllocated ) {
		Logger.info(Logger.APPLICATION_SOLIDUS, "Sending cancel request for Workflow.Errand(${errand.id})");
		if( Solidus.cancelRequest(errand.solidusOpenMediaID, cancelIfAllocated) ) {
			Logger.info(Logger.APPLICATION_SOLIDUS, "Cancel request returned true for Workflow.Errand(${errand.id})");
			errand.setValue('queuedInExternal', false);
			errand.setValue('solidusOpenMediaID', 0);
			errand.save();
			return true;
		} else {
			Logger.info(Logger.APPLICATION_SOLIDUS, "Cancel request returned false for Workflow.Errand(${errand.id})");
			return false;
		}
	}
	
	static function pullErrand( object errand ) {
		boolean failed = true;
		object status;
				
		Logger.info(Logger.APPLICATION_SOLIDUS, "Pulling errand ${errand.id} which is registered as ${errand.solidusOpenMediaID}");
		
		status = Solidus.getOpenMediaRequestStatus(errand.solidusOpenMediaID);
		if( status ) {
			if( status.OpenMediaRequests )  {
				if( status.OpenMediaRequests[0].Status == 'Queued' /* Queued at the Service Group */ ) {
					Logger.info(Logger.APPLICATION_SOLIDUS, "Status in Solidus for errand ${errand.id} (${errand.solidusOpenMediaID}) is 'Queued at the Service Group'");
					if( SolidusQueue._remove(errand, false) ) {
						Logger.info(Logger.APPLICATION_SOLIDUS, "Successfully canceled errand ${errand.id} in Solidus");
						failed = false;
					} else {
						Logger.info(Logger.APPLICATION_SOLIDUS, "Unable to cancel errand ${errand.id} (${errand.solidusOpenMediaID}) in Solidus");
					}
				} else if( status.OpenMediaRequests[0].Status == 'Complete' /* Completed */ ) {
					Logger.info(Logger.APPLICATION_SOLIDUS, "Status in Solidus for errand ${errand.id} (${errand.solidusOpenMediaID}) is 'Completed'");
					failed = false;
					errand.setValue('queuedInExternal', false);
					errand.setValue('solidusOpenMediaID', 0);
					errand.save();
				} else {
					// TODO: Log status message here
				}
			} else {
				Logger.info(Logger.APPLICATION_SOLIDUS, "Got status from Solius for errand ${errand.id} (${errand.solidusOpenMediaID}) but media had no previous requests registered");
			}
		} else {
			Logger.info(Logger.APPLICATION_SOLIDUS, "Unable to get status from Solidus for errand ${errand.id} (${errand.solidusOpenMediaID})");
			failed = false;
			errand.setValue('queuedInExternal', false);
			errand.setValue('solidusOpenMediaID', 0);
			errand.save();
		}
		
		return (not failed);
	}

	static function chatAgentsAvailable( object area ) {
		array list = Workflow.User.availableForChatList([ area.id ], false, 0);
		if( list )
			return list.size();
		return 0;
	}

	static function assignChatAgent( object area ) {
		object owner = null;

		array list = Workflow.User.availableForChatList([ area.id ], false, 0);
		if( list ) {
			array possibleOwners = [];

			number highestAmountOfAssignedChats = list[0]['count'];
			list.each() using ( row ) {
				if( row['count'] > highestAmountOfAssignedChats ) {
					highestAmountOfAssignedChats = row['count'];
				}
			};

			list.each() using ( row ) {
				if( row['count'] == highestAmountOfAssignedChats ) {
					possibleOwners[] = row['workflow_user_id'];
				}
			};
			if( possibleOwners ) {
				number randomNumber = Math.randomNumber(0, possibleOwners.size() - 1);
				number userID = possibleOwners[randomNumber];
				owner = Workflow.User.load(userID);
			}
		}

		return owner;
	}

	static function _addChat( object chat ) {
		object area = Workflow.Area.load(chat.area.id);
		object user = Workflow.User.load(chat.user.id);
		
		array activeChats = Workflow.ChatSession.activeList(user.systemGroup, Workflow.User.idListForUserFromCache(user.id), [ user.id ]);
		boolean hasActiveChat = (activeChats ? true : false);

		string privateData = (hasActiveChat ? '' : .SystemBaseURL + '/solidus/login/-/chat/' + chat.id);
		number preferredAgentID = user.solidusAgentID;
		boolean forceToPreferredAgent = true;
		array ivrInfo = [
			'Area' => chat.area.name,
			'Email' => (chat.client and chat.client.email
				? (chat.client.email.name
					? '{0} ({1})'.expand(chat.client.email.name, chat.client.email.emailAddress)
					: chat.client.email.emailAddress)
				: 'Unknown'),
			'Message' => (closure {
					object lastMessageFromClient = Workflow.ChatMessage.lastMessageFromClient(chat.id);
					return (lastMessageFromClient and lastMessageFromClient.message
						? (lastMessageFromClient.message.utf8Length() > 125
							? lastMessageFromClient.message.utf8Slice(125) + '...'
							: lastMessageFromClient.message)
						: 'No Message');
				}).invoke()
		];
		
		object result;
		
		Logger.info(Logger.APPLICATION_SOLIDUS, "Sending add request with preferredAgentID as ${preferredAgentID} for Workflow.ChatSession(${chat.id}).");
		
		result = Solidus.addRequest(area.solidusServiceGroupID, preferredAgentID, forceToPreferredAgent, privateData, ivrInfo);
		if( result and result.OpenMediaID > 0 ) {
			Logger.info(Logger.APPLICATION_SOLIDUS, "Workflow.ChatSession(${chat.id}) have been successfully queued as ${result.OpenMediaID}. Queue position is ${result.QueuePosition}, EWT is ${result.EWT}, request status is ${result.RequestStatus}.");
			if( result.RequestStatus == 'AllocatedToAgent' or result.RequestStatus == 'AgentProcessing' ) {
				chat.setValue('user', null);
				chat.setValue('queuedInExternal', true);
				chat.setValue('externalID', result.OpenMediaID);
				chat.save();
				return true;				
			} else {
				Solidus.cancelRequest(result.OpenMediaID, true, true);
				return false;
			}
		} else {
			Logger.info(Logger.APPLICATION_SOLIDUS, "Failed to add Workflow.ChatSession(${chat.id}) to queue (${result.RequestStatus}).");
			return false;
		}
	}

	static function addChat( object chat ) {
		object owner = chat.user;
		if( not ._addChat(session) ) {
			// We need to find another possible owner
			array activeAgents = Workflow.User.availableForChatList([ session.area.id ], false, 0);
			array possibleOwners = [];
			activeAgents.each() using ( row ) {
				if( row['workflow_user_id'] != owner.id )
					possibleOwners[] = row['workflow_user_id'];
			};
			if( possibleOwners ) {
				do {
					boolean result = false;
					owner = Workflow.User.load(possibleOwners.pop());
					if( user.solidusAgentID ) {
						session.setValue('user', owner);
						result = ._addChat(session);
					}
				} while( possibleOwners.size() > 0 and result == false );
				
				if( result and possibleOwners.size() == 0 ) {
					owner = null;
				}
			} else {
				owner = null;
			}
		}
		return (owner ? true : false);
	}

	static function removeChat( object chat ) {
		Solidus.cancelRequest(chat.externalID, true);
		chat.setValue('queuedInExternal', false);
		chat.save();
		return true;
	}
}

